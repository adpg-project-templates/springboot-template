plugins {
    id 'org.springframework.boot' version '3.5.9'
    id 'io.spring.dependency-management' version '1.1.6'
    id 'java'
    id "com.diffplug.spotless" version "8.1.0"
}

group = 'com.andrewpg'
version = '1.0.0'

java {
    sourceCompatibility = '17'
    targetCompatibility = '17'
}

repositories {
    mavenCentral()
}

dependencies {
    // Spring Boot Starters
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    // Spotless
    // JWT
    implementation 'io.jsonwebtoken:jjwt-api:0.12.6'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.6'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.6'

    // OpenAPI/Swagger
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.6.0'

    // Lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // MapStruct
    implementation 'org.mapstruct:mapstruct:1.6.2'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.6.2'
    // Necesario para que MapStruct y Lombok trabajen juntos
    annotationProcessor 'org.projectlombok:lombok-mapstruct-binding:0.2.0'

    // Database
    runtimeOnly 'org.postgresql:postgresql'

    // Hibernate Envers for entity auditing
    // Envers is included in hibernate-core, but we need to explicitly add it
    implementation 'org.hibernate.orm:hibernate-envers'

    // DevTools
    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    // Testing
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'

}

tasks.bootBuildImage {
    imageName = 'spring-boot-template:latest'
    environment = [
            'BP_DEV_MODE': 'true'
    ]
}

// Configuración de Spotless para formateo de código
spotless {
    java {
        target 'src/**/*.java'
        googleJavaFormat('1.23.0')
        removeUnusedImports()
        trimTrailingWhitespace()
        endWithNewline()
        lineEndings 'UNIX'

        // Excluir archivos generados y build
        targetExclude('build/**', '**/build/**', '**/generated/**')
    }

    format 'misc', {
        target '*.gradle', '*.md', '.gitignore'
        trimTrailingWhitespace()
        leadingTabsToSpaces(4)
        endWithNewline()
    }

    format 'xml', {
        target 'src/**/*.xml'
        trimTrailingWhitespace()
        leadingTabsToSpaces(4)
        endWithNewline()
    }

    format 'properties', {
        target 'src/**/*.properties'
        trimTrailingWhitespace()
        leadingTabsToSpaces(4)
        endWithNewline()
    }
}
